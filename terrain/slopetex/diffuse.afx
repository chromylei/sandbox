
technique [
    name = "diffuse"
    ps_main="ps_main"
    vs_main="vs_main"
    class_name="DiffuseEffect"
           ];

struct DirLight {
  vec4 dir;
  vec4 diffuse;
  vec4 ambient;
};

[uniform_func="SetPVW"] uniform mat4 pvw;
[uniform_func="SetWorld"] uniform mat4 world;
[uniform_func="SetPlatTex"] uniform Texture2D plat_tex;
[uniform_func="SetSlopeTex"] uniform Texture2D slope_tex;
[uniform_func="SetDirLight"] uniform DirLight light;

exchange struct VsOutput {
  [semantic="position" system="true"]
  vec4 position;
  vec2 coordtex;
  vec4 normal;
};

exchange struct VSInput {
  vec4 position;
  vec2 coordtex;
  vec4 normal;
};

VsOutput vs_main(VSInput input) {
  VsOutput o;
  o.position = pvw * input.position;
  o.coordtex = input.coordtex;
  o.normal = world * input.normal;
  return o;
}

[system="true" semantic="target"]
vec4 ps_main(VsOutput o) {
  
  float diffuse_factor = dot(normalize(o.normal.xyz), -light.dir.xyz);
  vec3 light_factor = light.ambient.xyz + light.diffuse.xyz * diffuse_factor;
  float dist = length(vec3(0.0, 1.0, 0.0) - o.normal.xyz);
  vec4 tex_diffuse;
  if (dist > 0.7) {
    tex_diffuse = sample2D(slope_tex, o.coordtex);
  } else if (dist < 0.2) {
    tex_diffuse = sample2D(plat_tex, o.coordtex);
  } else {
    tex_diffuse = sample2D(plat_tex, o.coordtex) * (1.0 - dist)
        + sample2D(slope_tex, o.coordtex) * dist;
  }
  return vec4(light_factor.xyz * tex_diffuse.xyz, 1.0);
}

